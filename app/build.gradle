plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}


def versionMajor = 0
def versionMinor = 0
def versionPatch = 8
def versionBuild = 4
def isStable = false
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        all {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
//    compileSdkPreview = "Tiramisu"
    compileSdk 32
    defaultConfig {
        applicationId "com.junkfood.seal"
        minSdk 26
//        targetSdkPreview "Tiramisu"
        targetSdk 32
        versionCode = versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        if (isStable) {
            versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        } else {
            versionName = "${versionMajor}.${versionMinor}.${versionPatch}-alpha0${versionBuild}"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    splits {
        abi {
            enable !project.hasProperty('noSplits')
            reset()
            include 'x86_64', 'arm64-v8a'
            universalApk false
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Seal-${defaultConfig.versionName}-${getFilter(ABI)}-${variant.buildType.name}.apk"
        }
    }

    kotlinOptions {
        freeCompilerArgs = freeCompilerArgs + "-opt-in=kotlin.RequiresOptIn"
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
        jniLibs.useLegacyPackaging = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.0-alpha04'
    implementation 'com.google.android.material:material:1.7.0-alpha02'
    implementation 'androidx.activity:activity-compose:1.4.0'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:$compose_md3_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.compose.animation:animation-graphics:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation("io.coil-kt:coil-compose:2.0.0-rc03")

    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt("androidx.room:room-compiler:$room_version")

//    implementation 'com.github.yausername.youtubedl-android:library:-SNAPSHOT'
//    implementation 'com.github.yausername.youtubedl-android:ffmpeg:-SNAPSHOT'
    implementation 'com.github.JunkFood02.youtubedl-android:ffmpeg:0.13.2'
    implementation 'com.github.JunkFood02.youtubedl-android:library:0.13.2'

    implementation 'com.tencent:mmkv:1.2.13'
    implementation "androidx.datastore:datastore-preferences:1.0.0"


    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.core:core-ktx:+"
}
